SPACE = _{ " " }
TAB = _{ "\t" }
WSPACE = _{ SPACE | TAB }
NEWLINE = _{ "\n" }
EMPTY_LINES = _{ NEWLINE{2,}+ | (NEWLINE ~ EOI) }
ONE_CHAR = _{ !NEWLINE ~ ANY }

comment = { "//" ~ ONE_CHAR* ~ NEWLINE }

id = @{ id_start_char ~ id_subsequent_char* }

id_start_char = @{ ASCII_ALPHA | "_" | ":" }

id_subsequent_char = @{
    ASCII_ALPHANUMERIC |
    "_" |
    "-" |
    "."
}

anchor = { inline_anchor ~ NEWLINE }
inline_anchor = _{
    ("[[" ~ id ~ ("," ~ xreflabel)? ~ "]]") |
    ("[" ~ "#" ~ id ~ ("," ~ xreflabel)? ~ "]")
}

xreflabel = { (!"]" ~ ANY)+ }

blocktitle = _{ "." ~ !("." | SPACE) ~ title ~ NEWLINE }
title = { (non_plain_text | one_line_plain_text)+ }

attribute_list = _{ attrlist ~ NEWLINE }

attrlist = _{
    // The case in which we keep the style empty
    open_sb ~ empty_style ~ "," ~ (attribute ~ ("," ~ attribute)*)? ~ close_sb |

    // The case in which style will be captured by a positional attribute
    open_sb ~ (attribute ~ ("," ~ attribute)* )? ~ close_sb
}

open_sb = { "[" }
close_sb = { "]" }

id_token = { "#" ~ id }

empty_style = { "" }
role = { (!"," ~ !"]" ~ !"#" ~ !"." ~ !"%" ~ ANY)+ }
option = { "\\\"" | (!"\"" ~ !"," ~ !"]" ~ !"#" ~ !"." ~ !"%" ~ ANY)+ }

// https://docs.asciidoctor.org/asciidoc/latest/attributes/names-and-values/#user-defined
//
// Although uppercase characters are permitted in an attribute name, the name is converted
// to lowercase before being stored. For example, URL and Url are treated as url. A best
// practice is to only use lowercase letters in the name and avoid starting the name with
// a number.
attribute_name = !{ (ASCII_ALPHANUMERIC | "-" | "_")+ }

attribute = _{ named_attribute | positional_attribute }
named_attribute = {
    ("id" ~ "=" ~ id) |
    ("role" ~ "=" ~ role) |
    (("options" | "opts") ~ "=" ~ (("\"" ~ option ~ ("," ~ option)* ~ "\"") | option)) |
    (attribute_name ~ "=" ~ named_attribute_value)
}

positional_attribute = _{
    positional_attribute_value ~ (("#" ~ id)? ~ (("." ~ role) | ("%" ~ option))*) |
    ("#" ~ id) ~ (("." ~ role) | ("%" ~ option))* |
    (("." ~ role) | ("%" ~ option))+
 }
named_attribute_value = {
    ("\"" ~ inner_attribute_value ~ "\"") |
    ( (!"," ~ !"]" ~ ANY)+ )
}
positional_attribute_value = { (!("\"" | "," | "]" | "#" | "." | "%") ~ ANY)+ }
inner_attribute_value = _{ ( "\\\"" | (!"\"" ~ ANY))* }

url = ${proto ~ "://" ~ path}
proto = ${
    "https" |
    "http" |
    "ftp" |
    "irc" |
    "mailto"
}
path = @{ (ASCII_ALPHANUMERIC | "_" | "-" | "." | "/" | "~" )+ }
