blocks = { meta_block* }

meta_block = _{
    NEWLINE* ~
    (
        document_attribute |
        block
    )
}

non_section_block = {
    (anchor | attribute_list | blocktitle)* ~ block_common
}

block_common = _{
    delimited_block |
    image_block |
    audio_block |
    video_block |
    toc_block |
    thematic_break_block |
    page_break_block |
    list |
    paragraph
}

block = {
    (anchor | attribute_list | blocktitle)* ~
    (section | block_common)
}

section = {
    section_header_start ~ section_title ~ NEWLINE{2} ~
    section_content*
}

section_header_start = _{ section_level ~ SPACE }

section_content = !{ (!(EOI | &blocks) ~ ANY)+ }

section_level_symbol = _{ ("=" | "#") }

section_level = { section_level_symbol{1,6} }
section_title = { inlines_inner }

image_block = { image ~ &NEWLINE? }
image = { "image::" ~ (url | path) ~ attrlist }

audio_block = { audio ~ &NEWLINE? }
audio = { "audio::" ~ (url | path) ~ attrlist }

video_block = { video ~ &NEWLINE? }
video = { "video::" ~ (url | path) ~ ("," ~ (url | path))* ~ attrlist }

toc_block = { "toc::[]" ~ &NEWLINE? }

thematic_break_block = @{
    (
        "'''" |
        // Below are the markdown-style ones
        "---" |
        "- - -" |
        "***" |
        "* * *"
    ) ~ &NEWLINE+
}

page_break_block = {
    page_break
}

page_break = _{ "<<<\n\n" }

// Paragraphs
paragraph = {
    admonition_node? ~ inlines ~ &(NEWLINE+ | EOI)
}

admonition_node = _{
    admonition ~ ": "
}

admonition = !{
    (
        "NOTE" |
        "TIP" |
        "IMPORTANT" |
        "WARNING" |
        "CAUTION"
    )
}
