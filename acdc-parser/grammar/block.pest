blocks = { meta_block* }

meta_block = _{
    NEWLINE* ~
    (
        document_attribute |
        block
    )
}

block = {
    (anchor | attribute_list | blocktitle)* ~
    (
        section |
        delimited_block |
        image_block |
        audio_block |
        video_block |
        thematic_break_block |
        page_break_block |
        list |
        paragraph
    )
}

section = {
    section_header_start ~ section_title ~ NEWLINE{2} ~
    section_content*
}

section_header_start = _{ section_level ~ SPACE }

section_content = !{ (!(EOI | &blocks) ~ ANY)+ }

section_level_symbol = _{ ("=" | "#") }

section_level = { section_level_symbol{1,6} }
section_title = { ONE_CHAR* }

image_block = { image ~ &NEWLINE }
image = { "image::" ~ (url | path) ~ attrlist }

audio_block = { audio ~ &NEWLINE }
audio = { "audio::" ~ (url | path) ~ attrlist }

video_block = { video ~ &NEWLINE }
video = { "video::" ~ (url | path) ~ ("," ~ (url | path))* ~ attrlist }

thematic_break_block = @{
    (
        "'''" |
        // Below are the markdown-style ones
        "---" |
        "- - -" |
        "***" |
        "* * *"
    ) ~ &NEWLINE+
}

page_break_block = {
    page_break
}

page_break = _{ "<<<\n\n" }

// Paragraphs
paragraph = {
    admonition_node? ~ paragraph_inner ~ &(NEWLINE+ | EOI)
}

paragraph_inner = {
    (
        non_plain_text ~ plain_text |
        plain_text
    )+
}

admonition_node = _{
    admonition ~ ": "
}

bold_text = { PUSH("*") ~ (!"*" ~ ANY)+ ~ POP }

inline_line_break = { " +" ~ NEWLINE }

non_plain_text = _{
    footnote |
    inline_line_break |
    bold_text
}

footnote = { "footnote:" ~ footnote_id? ~ "[" ~ footnote_text? ~ "]" }
footnote_id = { (!"[" ~ ANY)+ }
footnote_text = { (!"]" ~ ANY)+ }

admonition = !{
    (
        "NOTE" |
        "TIP" |
        "IMPORTANT" |
        "WARNING" |
        "CAUTION"
    )
}
plain_text = { (&(!(NEWLINE{2,} | EOI | non_plain_text)) ~ ANY)+ }
// WIP
//inline_element = {}
