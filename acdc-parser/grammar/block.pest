blocks = {
    (
        section |
        delimited_block |
        image_block |
        thematic_break_block |
        page_break_block |
        list |
        paragraph
    )*
}

section = {
    section_header_start ~ section_title ~ NEWLINE{2} ~
    section_content*
}

section_header_start = _{ section_level ~ SPACE }

section_content = !{ (!(&section_level | EOI | &delimited_block) ~ ANY)+ }

section_level_symbol = _{ ("=" | "#") }

section_level = { section_level_symbol{2,6} }
section_title = { ONE_CHAR* }

// TODO(nlopes): can we have multiple anchor statements? What does that mean? Multiple IDs
// or should I have an ID with multiple aliases/anchors?
//
// When working on anchors, check https://docs.asciidoctor.org/asciidoc/latest/attributes/id/#add-additional-anchors-to-a-section
image_block = { anchor* ~ image ~ (NEWLINE{2,} | EOI) }
image = { "image::" ~ (url | path) ~ attrlist }

//block_macro = { <name>"::" ~ target? ~ attrlist? }

thematic_break_block = @{
    (
        "'''\n\n" |
        // Below are the markdown-style ones
        "---\n\n" |
        "- - -\n\n" |
        "***\n\n" |
        "* * *\n\n"
    ) ~ NEWLINE*
}

page_break_block = {
    attribute_list ~ page_break ~ NEWLINE*
}

page_break = _{ "<<<\n\n" }

// Paragraphs
paragraph = {
    (anchor | attribute_list | blocktitle)* ~
    admonition_node? ~ paragraph_inner ~ NEWLINE*
}

paragraph_inner = {
    (
        non_plain_text ~ plain_text |
        plain_text
    )+
}

admonition_node = _{
    admonition ~ ": "
}

bold_text = { PUSH("*") ~ (!"*" ~ ANY)+ ~ POP }

non_plain_text = _{
    bold_text
}

admonition = !{
    (
        "NOTE" |
        "TIP" |
        "IMPORTANT" |
        "WARNING" |
        "CAUTION"
    )
}
plain_text = { (&(!(NEWLINE{2,} | EOI | non_plain_text)) ~ ANY)+ }
// WIP
//inline_element = {}
