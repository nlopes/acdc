// Lists
list = {
    (
        unordered_list
        | ordered_list
        // | description_list
    ) ~ &(NEWLINE+ | EOI)
}

list_item = ${
    (
        list_multiline |
        (continuation ~ delimited_block)
    )+
}

list_title = { "." ~ !("." | SPACE) ~ ONE_CHAR+ ~ (NEWLINE | EOI) }
list_multiline = ${ ((!(NEWLINE{2,} | EOI | NEWLINE ~ (unordered_level | ordered_level))  ~ ANY) | other_list_inline)+ }
other_list_inline = @{ (!EMPTY_LINES ~ !EOI ~ !list_inline ~ !(NEWLINE ~ (unordered_level | ordered_level)) ~ !(continuation ~ delimited_block) ~ ANY)+ }

continuation = { NEWLINE ~ "+" ~ NEWLINE }

// TODO(nlopes): only support one line for now
unordered_list = { unordered_list_item+ }
unordered_list_item = { unordered_level ~ SPACE ~ list_item ~ (NEWLINE | EOI) }
unordered_level = { ("*"+ | "-") }

ordered_list = { ordered_list_item+ }
ordered_list_item = { ordered_level ~ SPACE ~ checklist_item? ~ list_item ~ (NEWLINE | EOI) }
ordered_level = { ASCII_DIGIT* ~ "."+ }

checklist_item = _{ "[" ~ (checklist_item_checked | checklist_item_unchecked) ~ "]" ~ SPACE }
checklist_item_checked = !{ "X" | "x" | "*" }
checklist_item_unchecked = !{ SPACE }

description_list = { description_list_item+ }
description_list_item = {
    description_list_term_maybe_anchor ~
    description_list_delimiter ~
    (
        (SPACE ~ description_list_inline) |
        (NEWLINE ~ (!description_list ~ block)*)
    ) ~
    (NEWLINE | EOI)
}
description_list_term_maybe_anchor = _{ description_list_term_anchor ~ description_list_term }
description_list_term_anchor = { inline_anchor? }
description_list_term = { (!description_list_delimiter ~ ONE_CHAR)+ }
description_list_delimiter = { ":"{2,4} | ";;" }
// TODO(nlopes): this needs to be improved - still need to support text formatting etc
description_list_inline = { (!(NEWLINE | EOI) ~ ANY)+ }

list_inline = { ONE_CHAR* }
