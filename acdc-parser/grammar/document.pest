document_header = {
  document_attribute* ~
  (document_title_token ~ author_revision)? ~
  (document_attribute | comment)*
}

document_attribute = { document_attribute_entry }

// TODO(nlopes): I'd love to capture subtitle here but it's a lot trickier to find the
// last colon and capture everything up to it as the title and everything after it as the
// subtitle. So I'm just going to ignore it for now and instead do it in the parser.
document_title_token = @{ ("=" | "#") ~ SPACE ~ document_title ~ NEWLINE }
document_title = !{ ONE_CHAR+ }

document_attribute_entry = _{
    ":" ~
    (
        (unset ~ attribute_name) |
        (attribute_name ~ unset) |
        attribute_name
    ) ~
    ":" ~
    (SPACE ~ document_attribute_value)? ~
    NEWLINE
}
document_attribute_value = !{ ONE_CHAR* }

unset = { "!" }
author_revision = _{
  (author_line ~ revision_line?)?
}

author_line = _{ author ~ ("; " ~ author)* ~ NEWLINE }

author = {
    (author_first_name ~ optional_author_middle_name ~ author_last_name_token ~ optional_author_email) |
    (author_first_name ~ author_last_name_token ~ optional_author_email) |
    (author_first_name ~ optional_author_email) |
    (author_first_name ~ author_last_name_token)
 }

author_first_name = { name_part }
author_middle_name = { name_part }
author_last_name = { name_part }

optional_author_middle_name = _{ WSPACE+ ~ author_middle_name }
author_last_name_token = _{ WSPACE+ ~ author_last_name }

name_part = _{
  (ASCII_ALPHANUMERIC | "." | "-")+ ~
  ( "_" ~ (ASCII_ALPHANUMERIC | "." | "-")+ )*
}

optional_author_email = _{ WSPACE* ~ "<" ~ author_email ~ ">" }
author_email = { (!">" ~ ONE_CHAR)+ }

revision_line = {
  revision_number ~
  optional_revision_date ~
  optional_revision_remark ~
  NEWLINE
}

revision_number = { "v"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

optional_revision_date = _{ ("," ~ WSPACE ~ revision_date )? }
revision_date = { (!NEWLINE ~ !":" ~ ANY)+ }

optional_revision_remark = _{ (":" ~ WSPACE ~ revision_remark)? }
revision_remark = { ONE_CHAR+ }
