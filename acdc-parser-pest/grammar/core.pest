SPACE = _{ " " }
TAB = _{ "\t" }
WSPACE = _{ SPACE | TAB }
NEWLINE = _{ "\n" | "\r\n" }
EMPTY_LINE = _{ WSPACE* ~ NEWLINE }

empty_header = { "" ~ NEWLINE }
comment = { "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

id = @{ id_start_char ~ id_subsequent_char* }

id_start_char = @{ ASCII_ALPHA | "_" | ":" }

id_subsequent_char = @{
    ASCII_ALPHANUMERIC |
    "_" |
    "-" |
    "."
}

anchor = { inline_anchor ~ NEWLINE }
inline_anchor = {
    ("[[" ~ id ~ "]]") |
    ("[" ~ "#" ~ id ~ "]")
}

blocktitle = { "." ~ !"." ~ line ~ NEWLINE }
line = { (!NEWLINE ~ ANY)+ ~ ( &NEWLINE | &EOI) }

attribute_list = { inline_attribute_list ~ NEWLINE }

inline_attribute_list = _{
  "[" ~ (attribute ~ ("," ~ attribute)* )? ~ "]"
}

// https://docs.asciidoctor.org/asciidoc/latest/attributes/names-and-values/#user-defined
//
// Although uppercase characters are permitted in an attribute name, the name is converted
// to lowercase before being stored. For example, URL and Url are treated as url. A best
// practice is to only use lowercase letters in the name and avoid starting the name with
// a number.
attribute_name = !{ (ASCII_ALPHANUMERIC | "-" | "_")+ }

attribute = { named_attribute | attribute_value }
named_attribute = { attribute_name ~ "=" ~ attribute_value }
attribute_value = {
  ("\"" ~ inner_attribute_value ~ "\"") |
  ( (!"," ~ !"]" ~ ANY)+ )
}
inner_attribute_value = { ( "\\\"" | (!"\"" ~ ANY))* }
