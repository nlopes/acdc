document = _{
  SOI ~
  (EMPTY_LINE | comment)* ~
  (document_header ~ EMPTY_LINE)? ~
  (block ~ EMPTY_LINE{2}?)* ~
  (EMPTY_LINE | comment)* ~
  EOI
}

author_revision = _{
  (author_line ~ revision_line?)?
}

author_line = _{ author ~ ("; " ~ author)* ~ NEWLINE }

author = {
    (author_first_name ~ optional_author_middle_name ~ author_last_name_token ~ optional_author_email) |
    (author_first_name ~ author_last_name_token ~ optional_author_email) |
    (author_first_name ~ optional_author_email) |
    (author_first_name ~ author_last_name_token)
 }

author_first_name = { name_part }
author_middle_name = { name_part }
author_last_name = { name_part }

optional_author_middle_name = _{ WSPACE+ ~ author_middle_name }
author_last_name_token = _{ WSPACE+ ~ author_last_name }

name_part = _{
  (ASCII_ALPHANUMERIC | "." | "-")+ ~
  ( "_" ~ (ASCII_ALPHANUMERIC | "." | "-")+ )*
}

optional_author_email = _{ WSPACE* ~ "<" ~ author_email ~ ">" }
author_email = { (!">" ~ !NEWLINE ~ ANY)+ }

revision_line = {
  revision_number ~
  optional_revision_date ~
  optional_revision_remark ~
  NEWLINE
}

revision_number = { "v"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

optional_revision_date = _{ ("," ~ WSPACE ~ revision_date )? }
revision_date = { (!NEWLINE ~ !":" ~ ANY)+ }

optional_revision_remark = _{ (":" ~ WSPACE ~ revision_remark)? }
revision_remark = { (!NEWLINE ~ ANY)+ }


// Block Macros
block_macro = {
    macro_name ~ "::" ~ macro_target? ~ attribute_list? ~ NEWLINE
}

macro_name = {
    ASCII_ALPHANUMERIC+
}

macro_target = {
    (!attribute_list_start ~ ANY)+
}

// Attributes
attribute_list = {
    attribute_list_start ~ attribute* ~ attribute_list_end
}

attribute_list_start = {
    "["
}

attribute_list_end = {
    "]"
}

attribute = {
    attribute_name ~ ":" ~ macro_attribute_value? ~ ("," | attribute_list_end)
}

macro_attribute_value = {
    (!attribute_list_end ~ !"," ~ ANY)+
}
